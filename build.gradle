buildscript {
    repositories {
        mavenCentral()
        maven {
            url = 'https://repo.grails.org/grails/restricted'
        }
    }
    dependencies { // Not Published to Gradle Plugin Portal
        classpath platform("org.apache.grails:grails-bom:$grailsVersion")
        classpath "org.apache.grails:grails-data-hibernate5"
        classpath "org.apache.grails:grails-gradle-plugins"
    }
}

plugins {
    id "war"
    id "idea"
    id "java-library"
}
// Not Published to Gradle Plugin Portal
apply plugin: "org.apache.grails.gradle.grails-web"

version xhReleaseVersion

group 'io.xh'


repositories {
    mavenCentral()
    maven {
        url = 'https://repo.grails.org/grails/restricted'
    }
}

dependencies {

    //-------------------------------------------------------
    // Grails 7.0 REST Plugin Defaults + Other Grails Plugins
    //-------------------------------------------------------
    profile "org.apache.grails.profiles:plugin"
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    api platform("org.apache.grails:grails-bom:$grailsVersion")
    api "org.apache.grails:grails-async"
    api "org.apache.grails:grails-core"
    api "org.apache.grails:grails-data-hibernate5"
    api "org.apache.grails:grails-databinding"
    api "org.apache.grails:grails-events"
    api "org.apache.grails:grails-gsp"
    api "org.apache.grails:grails-interceptors"
    api "org.apache.grails:grails-logging"
    api "org.apache.grails:grails-services"
    api "org.apache.grails:grails-url-mappings"
    api "org.apache.grails:grails-web-boot"
    api "org.springframework.boot:spring-boot-autoconfigure"
    api "org.springframework.boot:spring-boot-starter-logging"
    api "org.springframework.boot:spring-boot-starter-tomcat"
    api "org.springframework.boot:spring-boot-starter-validation"
    api "org.grails.plugins:grails-mail:5.0.2"
    api "org.apache.grails:grails-quartz:4.0.0"

    //--------------------
    // Hoist Additions
    //--------------------
    api "com.hazelcast:hazelcast"
    implementation "javax.cache:cache-api"

    api "org.apache.tomcat:tomcat-jdbc"
    api "org.apache.groovy:groovy-dateutil"

    api "org.hibernate.orm:hibernate-jcache"

    api "com.esotericsoftware:kryo:5.6.2"
    api "com.github.java-json-tools:json-patch:1.13"
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    api "com.jayway.jsonpath:json-path:2.9.0"
    api "commons-io:commons-io:2.20.0"
    api "org.apache.directory.api:api-all:2.1.7"
    api "org.apache.httpcomponents.client5:httpclient5:5.5"
    api "org.apache.poi:ooxml-schemas:1.4"
    api "org.apache.poi:poi-ooxml-schemas:4.1.2"
    api "org.apache.poi:poi-ooxml:4.1.2"
    api "org.apache.poi:poi:4.1.2"
    api "org.jasypt:jasypt:1.9.3"
    api "org.owasp.encoder:encoder:1.3.1"
    api "org.springframework:spring-websocket"
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = ['-Dspring.output.ansi.enabled=always']
    }
}
bootJar.enabled = false

tasks.bootRun.doFirst {
    throw new RuntimeException('The bootRun task is not supported in hoist-core. If running in a "wrapper" config, ensure you run the task from your app, not the top-level wrapper.')
}
//------------------------
// Maven publishing
// This is a modified version of the gradle configs setup by the Grails plugin publishing plugin
// https://github.com/grails/grails-core/blob/master/grails-gradle-plugin/src/main/groovy/org/grails/gradle/plugin/publishing/GrailsCentralPublishGradlePlugin.groovy
// The default behavior assumes publishing of non-snapshot builds only to the paid (if private) Bintray system
//------------------------
java {
    withSourcesJar()
}

apply plugin:'maven-publish'
publishing {
    publications {
        hoistCore(MavenPublication) {
            artifactId 'hoist-core'

            pom.withXml {
                Node pomNode = asNode()
                if (pomNode.dependencyManagement) {
                    pomNode.dependencyManagement[0].replaceNode {}
                }

                pomNode.children().last() + {
                    delegate.name 'hoist-core'
                    delegate.description "Extremely Heavy Industry's toolkit for enterprise web applications."
                    delegate.url 'https://xh.io'
                    delegate.organization {
                        delegate.name 'Extremely Heavy Industries'
                        delegate.url 'https://xh.io'
                    }
                    delegate.scm {
                        delegate.url "https://github.com/xh/hoist-core"
                        delegate.connection "scm:git@github.com:xh/hoist-core.git"
                        delegate.developerConnection "scm:git@github.com:xh/hoist-core.git"
                    }
                    delegate.issueManagement {
                        delegate.system "GitHub"
                        delegate.url "https://github.com/xh/hoist-core/issues"
                    }
                    delegate.developers {
                        delegate.developer {
                            delegate.id 'xh'
                            delegate.name 'Extremely Heavy Industries'
                            delegate.email 'info@xh.io'
                        }
                    }
                }

                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }

            from components.java

            def groovyOutputDir = sourceSets.main.output.classesDirs.files.find{it.path.contains('/groovy/')}
            artifact source: "${groovyOutputDir}/META-INF/grails-plugin.xml",
                classifier: 'plugin',
                extension: 'xml'
        }
    }
    repositories {
        maven {
            name 'xhRepo'
            def repoEndpoint = version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'
            url "https://repo.xh.io/content/repositories/$repoEndpoint/"
            credentials {
                username project.findProperty('xhRepoDeployUser')
                password project.findProperty('xhRepoDeployPassword')
            }
        }
    }
}

task publishHoistCore(dependsOn: 'publishHoistCorePublicationToXhRepoRepository') {
    group 'xhio'
    description 'Publishes a build to repo.xh.io.'
    doLast {
        println "Hoist $version published to repo.xh.io!"
    }
}
