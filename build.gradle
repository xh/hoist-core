buildscript {
    repositories {
        mavenLocal()
        maven {url 'https://repo.grails.org/grails/core'}
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.grails.plugins:hibernate5:$gormVersion"
        classpath "org.grails.plugins:views-gradle:2.1.0"
    }
}

version xhReleaseVersion
group 'io.xh'

apply plugin:'idea'
apply plugin:'org.grails.grails-plugin'
apply plugin:'java-library'

repositories {
    mavenLocal()
    maven {url 'https://repo.grails.org/grails/core'}
}
configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

dependencies {
    //-----------------------------------
    // Grails 5.0 REST Plugin Defaults
    //-----------------------------------
    compileOnly "io.micronaut:micronaut-inject-groovy"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.grails:grails-core"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation "org.springframework.boot:spring-boot-starter-tomcat"
    implementation "org.grails:grails-plugin-url-mappings"
    implementation "org.grails:grails-plugin-rest"
    implementation "org.grails:grails-plugin-codecs"
    implementation "org.grails:grails-plugin-interceptors"
    implementation "org.grails:grails-plugin-services"
    implementation "org.grails:grails-plugin-datasource"
    implementation "org.grails:grails-plugin-databinding"
    implementation "org.grails:grails-web-boot"
    implementation "org.grails:grails-logging"
    implementation "org.grails.plugins:cache"
    implementation "org.grails.plugins:async"
    implementation "org.grails.plugins:hibernate5"
    profile "org.grails.profiles:rest-api-plugin"

    runtimeOnly "org.glassfish.web:el-impl:2.2.1-b05"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "org.apache.tomcat:tomcat-jdbc"
    runtimeOnly "javax.xml.bind:jaxb-api:2.3.1"


    //--------------------
    // Hoist Additions
    //--------------------
    api "org.codehaus.groovy:groovy-dateutil:3.0.9"
    api "org.grails:grails-plugin-domain-class"
    api "org.grails.plugins:mail:3.0.0"

    // Quartz, with Grails 4/5 workaround (https://github.com/grails-plugins/grails-quartz/issues/107
    api("org.quartz-scheduler:quartz:2.2.3") {exclude group: 'slf4j-api', module: 'c3p0'}
    api 'org.grails.plugins:quartz:2.0.13'

    api "org.hibernate:hibernate-core:5.5.7.Final"
    api "net.sf.ehcache:ehcache:2.10.9.2"

    api "org.apache.poi:poi:4.1.2"
    api "org.apache.poi:poi-ooxml:4.1.2"
    api "org.apache.poi:poi-ooxml-schemas:4.1.2"
    api "org.apache.poi:ooxml-schemas:1.4"

    api "org.springframework:spring-websocket"
    api "org.apache.httpcomponents.client5:httpclient5:5.1"
    api 'org.jasypt:jasypt:1.9.3'
    api "commons-io:commons-io:2.8.0"
    runtimeOnly "net.java.dev.jna:jna:4.5.1"   //TODO:  Is this needed?
    api "org.owasp.encoder:encoder:1.2.3"
}

def allJvmArgs = [
        '-Dspring.output.ansi.enabled=always'
]

bootRun {
    jvmArgs(allJvmArgs)
    sourceResources sourceSets.main
}

tasks.withType(GroovyCompile) {
    configure(groovyOptions) {
        forkOptions.jvmArgs = allJvmArgs
    }
}

bootJar.enabled = false

//------------------------
// Maven publishing
// This is a modified version of the gradle configs setup by the Grails plugin publishing plugin
// https://github.com/grails/grails-core/blob/master/grails-gradle-plugin/src/main/groovy/org/grails/gradle/plugin/publishing/GrailsCentralPublishGradlePlugin.groovy
// The default behavior assumes publishing of non-snapshot builds only to the paid (if private) Bintray system
//------------------------
apply plugin:'maven-publish'
publishing {
    publications {
        hoistCore(MavenPublication) {
            artifactId 'hoist-core'

            pom.withXml {
                Node pomNode = asNode()
                if (pomNode.dependencyManagement) {
                    pomNode.dependencyManagement[0].replaceNode {}
                }

                pomNode.children().last() + {
                    delegate.name 'hoist-core'
                    delegate.description "Extremely Heavy Industry's toolkit for enterprise web applications."
                    delegate.url 'https://xh.io'
                    delegate.organization {
                        delegate.name 'Extremely Heavy Industries'
                        delegate.url 'https://xh.io'
                    }
                    delegate.scm {
                        delegate.url "https://github.com/xh/hoist-core"
                        delegate.connection "scm:git@github.com:xh/hoist-core.git"
                        delegate.developerConnection "scm:git@github.com:xh/hoist-core.git"
                    }
                    delegate.issueManagement {
                        delegate.system "GitHub"
                        delegate.url "https://github.com/xh/hoist-core/issues"
                    }
                    delegate.developers {
                        delegate.developer {
                            delegate.id 'xh'
                            delegate.name 'Extremely Heavy Industries'
                            delegate.email 'info@xh.io'
                        }
                    }
                }

                pomNode.dependencies.dependency.findAll {
                    it.version.text().isEmpty()
                }.each {
                    it.replaceNode {}
                }
            }

            from components.java
            artifact sourcesJar
            //artifact javadocJar

            def groovyOutputDir = sourceSets.main.output.classesDirs.files.find{it.path.contains('/groovy/')}
            artifact source: "${groovyOutputDir}/META-INF/grails-plugin.xml",
                    classifier: 'plugin',
                    extension: 'xml'

            repositories {
                maven {
                    def repoEndpoint = version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'
                    url "https://repo.xh.io/content/repositories/$repoEndpoint/"
                    credentials {
                        username project.findProperty('xhRepoDeployUser')
                        password project.findProperty('xhRepoDeployPassword')
                    }
                }
            }
        }
    }
}

task publishHoistCore(dependsOn: 'publishHoistCorePublicationToMavenRepository') {
    group 'xhio'
    description 'Publishes a build to repo.xh.io.'
    doLast {
        println "Hoist $version published to repo.xh.io!"
    }
}
